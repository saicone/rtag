allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    repositories {
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://jitpack.io/' }
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:26.0.2'
    }

    compileJava {
        options.release = 11
        options.encoding = 'UTF-8'
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        options {
            links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
            links 'https://hub.spigotmc.org/javadocs/spigot/'
            links 'https://javadoc.io/doc/org.jetbrains/annotations/latest/'
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

subprojects {
    dependencies {
        implementation rootProject
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.9-R0.1-SNAPSHOT'
    compileOnly 'it.unimi.dsi:fastutil:8.5.16'
    compileOnly('com.github.Rubenicos:DataFixerUpper:8.0') { transitive = false }
}

def javadocProjects= [
        ':',
        ':rtag-block',
        ':rtag-entity',
        ':rtag-item'
]

tasks.register('allJavadoc', Javadoc) {
    source javadocProjects.collect { project(it).sourceSets.main.allJava }
    classpath = files(javadocProjects.collect { project(it).sourceSets.main.compileClasspath })
    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://javadoc.io/doc/org.jetbrains/annotations/latest/'
    }
    destinationDir = file(project.layout.buildDirectory.dir('docs/all-javadoc'))
}
